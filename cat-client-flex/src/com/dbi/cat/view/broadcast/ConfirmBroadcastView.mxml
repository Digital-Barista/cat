<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" 
                width="400" height="400"
                showCloseButton="true"
                close="onClose()" 
                creationComplete="init()"
                xmlns:components="com.dbi.cat.view.components.*"
                title="Select Networks">
    <mx:Script>
        <![CDATA[
            import com.dbi.cat.common.constants.EntryPointType;
            import com.dbi.cat.common.vo.CouponNodeVO;
            import com.dbi.cat.common.vo.CouponVO;
            import com.dbi.cat.common.vo.EntryDataVO;
            import com.dbi.cat.common.vo.EntryPointDefinitionVO;
            import com.dbi.cat.common.vo.MessageVO;
            import com.dbi.cat.common.vo.UserVO;
            import com.dbi.cat.common.vo.criteria.ContactSearchCriteriaVO;
            import com.dbi.cat.event.BroadcastMessageEvent;
            import com.dbi.controls.AdvancedComboBox;
            import com.dbi.controls.CustomMessage;
            import com.dbi.event.CustomMessageEvent;
            
            import mx.binding.utils.BindingUtils;
            import mx.collections.ArrayCollection;
            import mx.containers.HBox;
            import mx.controls.CheckBox;
            import mx.controls.ComboBox;
            import mx.controls.Label;
            import mx.controls.RadioButton;
            import mx.controls.RadioButtonGroup;
            import mx.controls.TextInput;
            import mx.validators.Validator;
            
            private const TYPE_MESSAGE:String = "Message";
            private const TYPE_COUPON:String = "Coupon";
            
            
            [Bindable]
            private var _message:MessageVO;
            public function set message(value:MessageVO):void
            {
                _message = value;
                dispatchEvent(new Event("messageUpdate"));
            }
            
            [Bindable]
            private var _coupon:CouponNodeVO;
            public function set coupon(value:CouponNodeVO):void
            {
                _coupon = value;
                dispatchEvent(new Event("messageUpdate"));
            }
            
            [Bindable(event="messageUpdate")]
            private function get messageType():String
            {
                if (_coupon != null)
                {
                    return TYPE_COUPON;
                }
                else if (_message != null)
                {
                    return TYPE_MESSAGE;
                }
                return "";
            }
            
            
            private function init():void
            {
                validateSend();
            }
            private function onClose():void
            {
                dispatchEvent(new BroadcastMessageEvent(BroadcastMessageEvent.CLOSE_CONFIRM_BROADCAST_MESSAGE));
            }
            
            
            private function confirmSend():void
            {
                var message:String = "";
                for each (var ed:EntryDataVO in EntryDataSelectForm.entryDatas)
                {
                    var number:String = "all";
                    if (ed.maxMessages > 0)
                    {
                        number = ed.maxMessages.toString();
                    }
                    
                    if (message.length > 0)
                    {
                        message += " and ";
                    }
                    message += number + " of your " + ed.entryType + " contacts";
                    
                }
                message = "Send this " + messageType + " to " + message + "?";
                
                CustomMessage.show(message,
                  ["Yes", "No"],
                  send);
            }
            
            private function send(e:CustomMessageEvent):void
            {
                if (e.ButtonText == "Yes")
                {
                    var event:BroadcastMessageEvent;
                    
                    if (messageType == TYPE_COUPON)
                    {
                        event = new BroadcastMessageEvent(BroadcastMessageEvent.SEND_BROADCAST_COUPON_MESSAGE);
                        event.coupon = _coupon;
                    }
                    else
                    {
                        event = new BroadcastMessageEvent(BroadcastMessageEvent.SEND_BROADCAST_MESSAGE);
                        event.message = _message;
                    }
                    event.clientID = EntryDataSelectForm.selectedClientId;
                    event.entryDatas = EntryDataSelectForm.entryDatas;
                    event.search = new ContactSearchCriteriaVO();
                    
                    dispatchEvent(event);
                    onClose();
                }
            }
            
            private function validateSend(e:Event=null):void
            {
                Send.enabled = EntryDataSelectForm.validateData();
            }
            
        ]]>
    </mx:Script>
    
    <mx:TextArea text="Select the networks this message should be sent out to"
                 fontSize="16" width="359" textAlign="center" borderStyle="none" backgroundAlpha="0.0" editable="false" x="16.5" top="10"/>
    <components:EntryDataSelect id="EntryDataSelectForm"
								change="validateSend()"  top="53" left="0" right="0" bottom="35"/>
    <mx:Button id="Send" label="Send {messageType}"
               left="5" right="5" bottom="5"
               click="confirmSend()" />
</mx:TitleWindow>
