<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
	width="100%" height="100%"
	label="{entryType}">
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import com.dbi.cat.common.vo.ClientVO;
			import com.dbi.cat.common.vo.AddInMessageVO;
			import com.dbi.cat.common.constants.AddInMessageType;
			
			[Bindable]
			public var editable:Boolean = true;
			
			private var _currentClient:ClientVO;
			[Bindable]
			public function get currentClient():ClientVO
			{
				return _currentClient;
			}
			public function set currentClient(c:ClientVO):void
			{
				_currentClient = c;
				dispatchEvent(new Event("dataUpdate"));	
			}
			
			private var _entryType:String;
			[Bindable]
			public function get entryType():String
			{
				return _entryType;
			}
			public function set entryType(value:String):void
			{
				_entryType = value;
				dispatchEvent(new Event("dataUpdate"));	
			}
			
			
			[Bindable(event="dataUpdate")]
			private function get addInMessage():AddInMessageVO
			{
				if (currentClient != null &&
					currentClient.addInMessages != null &&
					entryType != null)
				{
					for each (var add:AddInMessageVO in currentClient.addInMessages)
					{
						if (add.entryType == entryType &&
							add.type == AddInMessageType.CLIENT)
							return add;
					}
				}
				return null;
			}
			
			private function changeAddInMessage():void
			{
				if (addInMessage != null)
				{
					addInMessage.message = AddIn.text;
				}
				else
				{
					var add:AddInMessageVO = new AddInMessageVO();
					add.clientId = currentClient.clientId.toString();
					add.entryType = entryType;
					add.type = AddInMessageType.CLIENT;
					add.message = AddIn.text;
					
					if (currentClient.addInMessages == null)
						currentClient.addInMessages = new ArrayCollection();
					currentClient.addInMessages.addItem(add);
				}
			}
		]]>
	</mx:Script>
	<mx:TextArea id="AddIn"
		borderStyle="none"
		editable="{this.editable}"
		width="100%"
		change="changeAddInMessage()"
		text="{addInMessage.message}" />
</mx:Canvas>
