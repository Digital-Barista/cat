<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%"
	verticalGap="0">
	<mx:Script>
		<![CDATA[
			import com.dbi.cat.common.constants.ContactTagType;
			import mx.events.CollectionEvent;
			import mx.collections.HierarchicalData;
			import mx.controls.treeClasses.TreeListData;
			import com.dbi.cat.common.vo.ClientVO;
			import com.dbi.cat.common.vo.ContactTagVO;
			import com.dbi.cat.event.ContactEvent;
			import mx.collections.ArrayCollection;
			
			public var selectedClient:ClientVO;
			
			private var _contactTags:ArrayCollection;
			[Bindable]
			public function get contactTags():ArrayCollection
			{
				return _contactTags;
			}
			public function set contactTags(value:ArrayCollection):void
			{
				if (contactTags != null)
					contactTags.removeEventListener(CollectionEvent.COLLECTION_CHANGE, contactUpdate);
				
				_contactTags = value;
				contactTags.addEventListener(CollectionEvent.COLLECTION_CHANGE, contactUpdate);
				setupTagLists();
			}
			
			private var userTags:ArrayCollection;
			private var systemTags:ArrayCollection;
			
			[Bindable(event="dataUpdated")]
			private var tagTreeData:HierarchicalData;
			
			private function setupTagLists():void
			{
				userTags = new ArrayCollection();
				systemTags = new ArrayCollection();
				
				for each (var ct:ContactTagVO in contactTags)
				{
					if (ct.type == ContactTagType.USER)
						userTags.addItem(ct);
					else
						systemTags.addItem(ct);
				}
				
				tagTreeData = new HierarchicalData(new ArrayCollection(
					[{tag:'User', children:userTags},
					{tag:'System', children:systemTags}]));
			
				dispatchEvent(new Event("dataUpdated"));			
			}
			
			private function contactUpdate(e:CollectionEvent):void
			{
				setupTagLists();
			}
			private function addTag():void
			{
				if (Tag.text.length > 0)
				{
					var tag:ContactTagVO = new ContactTagVO();
					tag.tag = Tag.text;
					tag.type = ContactTagType.USER;
					tag.clientId = selectedClient.clientId;
					
					var event:ContactEvent = new ContactEvent(ContactEvent.SAVE_CONTACT_TAG);
					event.contactTag = tag;
					dispatchEvent(event);
				}
			}
		]]>
	</mx:Script>
	<mx:Label text="New Tag" fontWeight="bold" />
	<mx:HBox width="100%" verticalAlign="middle">
		<mx:TextInput id="Tag" width="100%" />
		<mx:Image source="@Embed(source='/assets/swf/plus.swf')"
			height="18" width="18"
			click="addTag()" />
	</mx:HBox>
	<mx:Spacer height="5" />
	<mx:AdvancedDataGrid id="Tags"
		dataProvider="{tagTreeData}" 
		displayItemsExpanded="true"
		showHeaders="false"
		width="100%" height="100%" >
		<mx:columns>
			<mx:AdvancedDataGridColumn itemRenderer="com.dbi.cat.view.contacts.TagItemRenderer" dataField="tag" />
		</mx:columns>
	</mx:AdvancedDataGrid>
</mx:VBox>
