<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" xmlns:contacts="com.dbi.cat.view.contacts.*">
	<mx:Script>
		<![CDATA[
			import mx.utils.ObjectUtil;
			import mx.collections.ArrayCollection;
			import com.dbi.cat.common.constants.ContactTagType;
			import com.dbi.cat.common.vo.ContactTagVO;
			import mx.collections.HierarchicalData;
			import mx.events.CollectionEvent;
			
			
			private var _contactTagLists:ArrayCollection;
			[Bindable]
			public function get contactTagLists():ArrayCollection
			{
				return _contactTagLists;
			}
			public function set contactTagLists(value:ArrayCollection):void
			{
				// Make a local copy so "selected" flag is per TagChooser
				if (value != null)
				{
					// Preserve selected flags of current tag list
					var old:ArrayCollection = _contactTagLists;
					_contactTagLists = ObjectUtil.copy(value) as ArrayCollection;
					
					if (old != null)
					{
						for each (var newObject:Object in contactTagLists)
						{
							for each (var newTag:ContactTagVO in newObject.children)
							{
								for each (var oldObject:Object in old)
								{
									for each (var oldTag:ContactTagVO in oldObject.children)
									{
										if (oldTag.contactTagId == newTag.contactTagId)
										{
											newTag.selected = oldTag.selected;
											break;
										}
									}
								}
							}	
						}
					}
				}
			}
			
			
			public function unselectAll():void
			{
				if (contactTagLists != null)
				{
					for each (var o:Object in contactTagLists)
					{
						for each (var tag:ContactTagVO in o.children)
						{
							tag.selected = false;
						}
					}
				}
			}
			public function get selectedTags():ArrayCollection
			{
				var ret:ArrayCollection = new ArrayCollection();
				if (contactTagLists != null)
				{
					for each (var o:Object in contactTagLists)
					{
						for each (var tag:ContactTagVO in o.children)
						{
							if (tag.selected)
								ret.addItem(tag);
						}
					}
				}
				return ret;
			}
		]]>
	</mx:Script>
	<mx:Repeater id="TagRepeater"
		dataProvider="{contactTagLists}">
		<mx:Label text="{TagRepeater.currentItem.tag}"
			fontWeight="bold" fontSize="14" />
		<contacts:TagTileList id="TagTileList"
			dataProvider="{TagRepeater.currentItem.children}"
			borderStyle="none"
			selectable="false"
			columnCount="3" rowHeight="30"
			horizontalScrollPolicy="off"
			itemRenderer="com.dbi.cat.view.contacts.TagItemRenderer"
			width="100%"  />
	</mx:Repeater>
</mx:VBox>
