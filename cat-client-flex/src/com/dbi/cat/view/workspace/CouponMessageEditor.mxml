<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" xmlns:workspace="com.dbi.cat.view.workspace.*"
		   creationComplete="init()">
	<mx:Metadata>
		[Event(type="flash.events.Event", name="change")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import com.dbi.cat.common.constants.EntryPointType;
			import com.dbi.cat.common.vo.CouponNodeVO;
			import com.dbi.cat.common.vo.CouponVO;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Button;
			
			[Bindable]
			public var showingAvailable:Boolean = true;
			
			private var _couponToken:String;
			[Bindable]
			public function get couponToken():String
			{
				return _couponToken;
			}
			public function set couponToken(value:String):void
			{
				_couponToken = value;
			}
			
			private var _couponVO:CouponNodeVO;
			[Bindable]
			public function get couponVO():CouponNodeVO
			{
				return _couponVO;
			}
			public function set couponVO(value:CouponNodeVO):void
			{
				_couponVO = value;
				
				if (couponVO)
				{
					if (!couponVO.availableMessages)
						couponVO.availableMessages = new Object();
					
					if (!couponVO.unavailableMessages)
						couponVO.unavailableMessages = new Object();
				}
			}
			
			private function init():void
			{
				updateRepeaterItems();
			}
			
			private function changeMessageShowing():void
			{
				showingAvailable = AvailableGroup.selection == Available;
				
				// Update tab coloring
				tabChange();
			}
			
			public function updateRepeaterItems():void
			{
				if (AvailableMessageType != null &&
				    AvailableMessageType.length == EntryPointType.allTypes.length)
				{
					for (var i:int = 0; i < EntryPointType.allTypes.length; i++)
					{
						var type:EntryPointType = EntryPointType.allTypes[i];
						var editor:MessageEditor = AvailableMessageType[i];
						editor.messageText = couponVO.availableMessages[type.name];
					}
				}
			}
			
			private function updateDefaultAvailable():void
			{
				couponVO.availableMessage = DefaultAvailable.messageText;
				dispatchEvent(new Event(Event.CHANGE));
			}
			
			private function updateDefaultUnavailable():void
			{
				couponVO.unavailableMessage = DefaultUnavailable.messageText;
				dispatchEvent(new Event(Event.CHANGE));
			}
			
			private function updateAvailableMessageType(text:String, entryType:EntryPointType):void
			{
				couponVO.availableMessages[entryType.name] = text;
				dispatchEvent(new Event(Event.CHANGE));
				
			}
			private function updateUnavailableMessageType(text:String, entryType:EntryPointType):void
			{
				couponVO.unavailableMessages[entryType.name] = text;
				dispatchEvent(new Event(Event.CHANGE));
			}
			
			/**
			 * Update tab color if the message is not the default
			 */
			private function tabChange():void
			{
				var types:ArrayCollection = EntryPointType.allTypes;
				for (var i:Number =0; i < types.length; i++)
				{
					var type:EntryPointType = types[i];
					
					// Update available tabs
					if (AvailableEntryTypeTabs)
					{
						var availableTab:Button = AvailableEntryTypeTabs.getTabAt(i+1);
						if (couponVO.availableMessages != null &&
							couponVO.availableMessages[type.name] != null &&
							couponVO.availableMessages[type.name].length > 0)
						{
							availableTab.setStyle("color", 0xFF0000);
						}
						else
						{
							availableTab.setStyle("color", null);
						}
					}
					
					// Update unavailable tabs
					if (UnavailableEntryTypeTabs &&
						UnavailableEntryTypeTabs.numChildren == types.length + 1)
					{
						var unavailableTab:Button = UnavailableEntryTypeTabs.getTabAt(i+1);
						if (couponVO.unavailableMessages != null &&
							couponVO.unavailableMessages[type.name] != null &&
							couponVO.unavailableMessages[type.name].length > 0)
						{
							unavailableTab.setStyle("color", 0xFF0000);
						}
						else
						{
							unavailableTab.setStyle("color", null);
						}
					}
				}
				dispatchEvent(new Event('changeMessage'));
			}
		]]>
	</mx:Script>
	<mx:RadioButtonGroup id="AvailableGroup"
						 change="{changeMessageShowing()}"/>
	<mx:HBox>
		<mx:RadioButton id="Available"
						label="Available Message"
						group="{AvailableGroup}"
						selected="{showingAvailable}" />
		<mx:RadioButton id="Unavailable"
						label="Unavailable Message"
						group="{AvailableGroup}"
						selected="{!showingAvailable}" />
	</mx:HBox>
	
	<mx:ViewStack selectedIndex="{showingAvailable ? 0 : 1}" right="0" left="0" top="0" bottom="0">
		<mx:TabNavigator id="AvailableEntryTypeTabs"
						 width="100%" height="100%"
						 tabOffset="-10" paddingTop="0"
						 horizontalAlign="right"
						 creationPolicy="all"
						 change="{tabChange()}"
						 creationComplete="{tabChange()}">
			<workspace:MessageEditor id="DefaultAvailable"
									 label="Default Message"
									 useModifyMessage="false"
									 messageText="{couponVO.availableMessage}"
									 change="updateDefaultAvailable()" />
			<mx:Repeater id="AvailableEntryTypeRepeater" 
						 dataProvider="{EntryPointType.allTypes}">
				<workspace:MessageEditor id="AvailableMessageType"
										 label="{AvailableEntryTypeRepeater.currentItem.name}"
										 defaultMessageText="{couponVO.availableMessage}"
										 messageText="{couponVO.availableMessages[AvailableEntryTypeRepeater.currentItem.name]}"
										 change="{updateAvailableMessageType(event.currentTarget.messageText, event.currentTarget.getRepeaterItem())}" />
			</mx:Repeater>
		</mx:TabNavigator>
		<mx:TabNavigator id="UnavailableEntryTypeTabs"
						 width="100%" height="100%"
						 tabOffset="-10" paddingTop="0"
						 horizontalAlign="right"
						 creationPolicy="all"
						 change="{tabChange()}">
			<workspace:MessageEditor id="DefaultUnavailable"
									 label="Default Message"
									 useModifyMessage="false"
									 messageText="{couponVO.unavailableMessage}"
									 change="updateDefaultUnavailable()" />
			<mx:Repeater id="UnavailableEntryTypeRepeater" 
						 dataProvider="{EntryPointType.allTypes}">
					<workspace:MessageEditor id="UnavailableMessageType"
											 label="{UnavailableEntryTypeRepeater.currentItem.name}"
											 defaultMessageText="{couponVO.unavailableMessage}"
											 messageText="{couponVO.unavailableMessages[UnavailableEntryTypeRepeater.currentItem.name]}"
											 change="{updateUnavailableMessageType(event.currentTarget.messageText, event.currentTarget.getRepeaterItem())}" />
			</mx:Repeater>
		</mx:TabNavigator>
	</mx:ViewStack>
	
</mx:Canvas>
