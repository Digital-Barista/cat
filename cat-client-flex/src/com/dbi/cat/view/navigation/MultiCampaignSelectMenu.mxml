<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml"
		 styleName="campaignSelectMenu"
		 creationComplete="init()"
		 paddingBottom="10" paddingLeft="10" paddingRight="10" paddingTop="10">
	<mx:Metadata>
		[Event(name="close", type="flash.events.Event")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import com.dbi.cat.common.vo.ClientVO;
			import com.dbi.cat.event.ClientEvent;
			
			import mx.collections.ArrayCollection;
			import mx.controls.CheckBox;
			import mx.utils.ObjectUtil;
			
			[Bindable]
			public var clients:ArrayCollection;
			
			private var _filterClients:ArrayCollection;
			public function set filterClients(value:ArrayCollection):void
			{
				_filterClients = value;
			}
			
			private var localFilterClients:ArrayCollection;
			
			private function init():void
			{
				Clients.removeAllChildren();
				for each (var client:ClientVO in clients)
				{
					var check:CheckBox = new CheckBox();
					check.label = client.name;
					check.data = client;
					check.selected = _filterClients != null && _filterClients.contains(client);
					Clients.addChild(check);
				}
			}
				
			private function changeType():void
			{
				if (SelectAll.selection == All)
				{
					// Null out to signify "all" clients
					localFilterClients = null;
				}
				else if (SelectAll.selection == Select)
				{
					// Initialize collection if necessary to signify no selected clients
					localFilterClients = _filterClients ? _filterClients as ArrayCollection : new ArrayCollection();
				}
			}
			
			private function applyClients():void
			{
				var event:ClientEvent = new ClientEvent(ClientEvent.CHANGE_FILTER_CLIENTS);
				if (SelectAll.selection == Select)
				{
					event.clients = new ArrayCollection();
					
					// Add all checked clients
					for each (var check:CheckBox in Clients.getChildren())
					{
						if (check.selected)
						{
							event.clients.addItem(check.data);
						}
					}
				}
				
				dispatchEvent(event);
				
				dispatchEvent(new Event("close"));
			}
		]]>
	</mx:Script>
	<mx:RadioButtonGroup id="SelectAll" change="changeType()" />
	<mx:RadioButton id="All" label="All Clients" group="{SelectAll}" selected="true" />
	<mx:RadioButton id="Select" label="Selected Clients" group="{SelectAll}" />
	<mx:HRule width="100%" />
	<mx:VBox id="Clients"
			 enabled="{SelectAll.selection == Select}" />
	<mx:Button label="Apply" width="100%"
			   click="applyClients()"/>
</mx:VBox>
